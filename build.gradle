plugins {
    id 'java-library'
    id 'maven-publish'
    id 'idea'
    id 'net.neoforged.moddev' version '2.0.110'
}

neoForge.version = "21.9.3-beta"
version = "0.20.0"
group= "dev.gigaherz.packingtape"
base.archivesName = "PackingTape-${neoForge.minecraftVersion}"

java.toolchain.languageVersion = JavaLanguageVersion.of(21)

sourceSets {
    main.resources.srcDirs += 'src/main/generated_resources'
}

repositories {
    mavenLocal()
}

neoForge {
    runs {
        // applies to all the run configs below
        configureEach {
            gameDirectory = project.file('run')
            logLevel = org.slf4j.event.Level.DEBUG
            //jvmArgument "-Dmixin.debug.export=true"
            //jvmArgument "-Dmixin.debug.verbose=true"
        }

        client {
            client()
        }

        server {
            server()
        }

        clientData {
            clientData()
            programArguments.addAll '--mod', 'packingtape', '--all', '--output', file('src/main/generated_resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()
        }
    }

    mods {
        thismod {
            sourceSet(sourceSets.main)
        }
    }

    parchment {
        minecraftVersion = "1.21.8"
        mappingsVersion = "BLEEDING-SNAPSHOT"
    }
}

dependencies {
}

jar {
    from(files('LICENSE.txt'))

    manifest {
        attributes([
                "Specification-Title": "packingtape",
                "Specification-Vendor": "gigaherz",
                "Specification-Version": "1", // We are version 1 of ourselves
                "Implementation-Title": project.name,
                "Implementation-Version": project.version,
                "Implementation-Vendor" :"gigaherz",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
}

// IDEA no longer automatically downloads sources/javadoc jars for dependencies, so we need to explicitly enable the behavior.
idea {
    module {
        downloadSources = true
        downloadJavadoc = true
    }
}
