buildscript {
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
    }
}

plugins {
    id 'com.github.johnrengelman.shadow' version '1.2.3'
}

repositories {
    maven {
        url "http://dvs1.progwml6.com/files/maven"
    }
    maven {
        url 'http://dogforce-games.com/maven'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'maven-publish'

version = "0.6.2"
group= "gigaherz.packingtape" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "PackingTape-1.11.2"

minecraft {
    version = "1.11.2-13.20.0.2298"
    runDir = "run"

    mappings = "snapshot_20170518"

    replace "@VERSION@", project.version
    replaceIn "ModPackingTape.java"
}

jar {
    classifier = 'slim'
}

shadowJar {
    classifier = ''
    dependencies {
        include(dependency(':Commons:'))
        exclude '*CraftTweaker*'
    }
    relocate 'gigaherz.common', 'gigaherz.packingtape.shadow.common'
}

reobf {
    shadowJar { mappingType = 'SEARGE' }
}

tasks.build.dependsOn reobfShadowJar

artifacts {
    archives shadowJar
    archives sourceJar
}

dependencies {
    deobfCompile "gigaherz.commons:Commons:1.11.0-0.4.5"
}

compileJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    options.encoding = 'UTF-8'
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

tasks.publish.dependsOn build
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourceJar
        }
    }

    repositories {
        maven {
            url "$buildDir/repo"
        }
    }
}

idea { module.inheritOutputDirs = true }
